group "org.polypheny"


dependencies {
    compileOnly project(":core")

    implementation(group: "org.apache.pig", name: "pig", version: pig_version, classifier: "h2") {
        exclude(group: "org.slf4j")
    }  // Apache 2.0


    // --- Test Compile ---
    testImplementation project(path: ":core", configuration: "tests")
    testImplementation project(path: ":core")

    testImplementation group: "junit", name: "junit", version: junit_version
    testImplementation group: "org.hamcrest", name: "hamcrest-core", version: hamcrest_core_version  // BSD 3-clause

    testImplementation group: "org.apache.hadoop", name: "hadoop-client", version: hadoop_client_version  // Apache 2.0
    testImplementation group: "org.apache.hadoop", name: "hadoop-common", version: hadoop_common_version  // Apache 2.0
    testImplementation group: "org.apache.pig", name: "pigunit", version: pigunit_version  // Apache 2.0

    // due to gradle 7.6+
    testImplementation project(path: ':dbms', configuration: 'tests')
}


sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
            outputDir = file(project.buildDir.absolutePath + "/classes")
        }
        resources {
            srcDirs = ["src/main/resources"]
        }
        output.resourcesDir = file(project.buildDir.absolutePath + "/classes")
    }
    test {
        java {
            srcDirs = ["src/test/java"]
            outputDir = file(project.buildDir.absolutePath + "/test-classes")
        }
        resources {
            srcDirs = ["src/test/resources"]
        }
        output.resourcesDir = file(project.buildDir.absolutePath + "/test-classes")
    }
}

delombok {
    dependsOn(":core:processResources")
}

compileJava {
    dependsOn(":core:processResources")
}

javadoc {
    dependsOn(":core:processResources")
}


/**
 * Tests
 */
test {
    include "**/PigRelBuilderStyleTest.class", "**/PigAdapterTest.class"
}


/**
 * JARs
 */
jar {
    manifest {
        attributes "Manifest-Version": "1.0"
        attributes "Copyright": "The Polypheny Project (polypheny.org)"
        attributes "Version": "$project.version"
    }
}
java {
    withJavadocJar()
    withSourcesJar()
}

licensee {

    ignoreDependencies('org.apache.pig') {
        transitive = true
        because "removed on release branches"
    }
}




