group "org.polypheny"


dependencies {
    compileOnly project(":core")

    // Apache 2.0
    implementation(group: "org.web3j", name: "core", version: web3j_version) {
        exclude(group: "org.slf4j")
        exclude(group: "com.fasterxml.jackson.core")
    }


    // --- Test Compile ---
    testImplementation project(path: ":core", configuration: "tests")
}


sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
        }
        resources {
            srcDirs = ["src/main/resources"]
        }
        output.resourcesDir = file(project.buildDir.absolutePath + "/classes")
    }
    test {
        java {
            srcDirs = ["src/test/java"]
            destinationDirectory.set(file(project.buildDir.absolutePath + "/test-classes"))
        }
        resources {
            srcDirs = ["src/test/resources"]
        }
        output.resourcesDir = file(project.buildDir.absolutePath + "/test-classes")
    }
}

compileJava {
    dependsOn(":config:processResources")
    dependsOn(":core:processResources")
    dependsOn(":information:processResources")
}

delombok {
    dependsOn(":core:processResources")
}


/**
 * JARs
 */
jar {
    manifest {
        attributes "Manifest-Version": "1.0"
        attributes "Copyright": "The Polypheny Project (polypheny.org)"
        attributes "Version": "$project.version"
    }
}
java {
    withJavadocJar()
    withSourcesJar()
}

licensee {
    allow('Apache-2.0')
    allow('MIT')
    allow('CC0-1.0')

    allowDependency('com.github.jnr', 'jnr-posix', '3.0.47') { because 'Eclipse Public License v. 2.0' }

    allowDependency('org.bouncycastle', 'bcprov-jdk15on', '1.65') { because 'MIT license' }

    allowDependency('org.java-websocket', 'Java-WebSocket', '1.3.8') { because 'MIT license' }

    allowDependency('org.ow2.asm', 'asm-util', '5.0.3') { because 'removed on release branches' }
    allowDependency('org.ow2.asm', 'asm', '5.0.3') { because 'BSD 3-Clause' }
    allowDependency('org.ow2.asm', 'asm-analysis', '5.0.3') { because 'BSD 3-Clause' }
    allowDependency('org.ow2.asm', 'asm-commons', '5.0.3') { because 'BSD 3-Clause' }
    allowDependency('org.ow2.asm', 'asm-tree', '5.0.3') { because 'BSD 3-Clause' }
}
