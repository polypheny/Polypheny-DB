plugins {
    id "org.jetbrains.kotlin.jvm" version "1.7.22"
}

group "org.polypheny"

apply plugin: "kotlin"

dependencies {
    compileOnly project(":core")
    compileOnly project(":plugins:sql-language")

    implementation(group: "io.grpc", name: "grpc-all", version: cottontaildb_grpc_version) {
        exclude group: "junit"
    }
    // Cottontail DB
    implementation(group: "org.vitrivr", name: "cottontaildb", version: cottontaildb_version) {
        exclude group: "junit"
    }
    implementation(group: "org.vitrivr", name: "cottontaildb-proto", version: cottontaildb_driver_version) {
        exclude group: "junit"
    }

    implementation group: "org.slf4j", name: "log4j-over-slf4j", version: slf4j_api_version

    // --- Test Compile ---
    // The line below causes issues, because it leads to executing :dbms:compileTestJava, even when building with -xtest
    //testImplementation project(path: ":dbms", configuration: "test")
    testImplementation project(path: ":dbms")
    testImplementation project(path: ":core", configuration: "tests")
    testImplementation project(path: ":core")

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: junit_jupiter_version

}

compileKotlin {
    dependsOn(":plugins:sql-language:processResources")
}

compileJava {
    dependsOn(processResources)
    dependsOn(":plugins:sql-language:processResources")
}

delombok {
    dependsOn(":plugins:sql-language:processResources")
}

inspectClassesForKotlinIC {
    dependsOn(":plugins:cottontail-adapter:compileTestKotlin")
}

plugin {
    dependsOn(":plugins:cottontail-adapter:compileTestKotlin")
}


sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
        }
        resources {
            srcDirs = ["src/main/resources"]
        }
        output.resourcesDir = file(project.buildDir.absolutePath + "/classes")
    }
    test {
        java {
            srcDirs = ["src/test/java"]
            destinationDirectory.set(file(project.buildDir.absolutePath + "/test-classes"))
        }
        resources {
            srcDirs = ["src/test/resources"]
        }
        output.resourcesDir = file(project.buildDir.absolutePath + "/test-classes")
    }
}


/**
 * JARs
 */
jar {
    manifest {
        attributes "Manifest-Version": "1.0"
        attributes "Copyright": "The Polypheny Project (polypheny.org)"
        attributes "Version": "$project.version"
    }
    dependsOn(":plugins:cottontail-adapter:compileTestKotlin")
}
java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    dependsOn(":plugins:cottontail-adapter:compileTestKotlin")
}

licensee {
    allow('Apache-2.0')
    allow('MIT')
    allow('BSD-3-Clause')

    allow('EPL-1.0')

    allowUrl('https://golang.org/LICENSE')

    allowDependency('org.bouncycastle', 'bcpkix-jdk15on', '1.67') { because 'MIT license' }
    allowDependency('org.bouncycastle', 'bcprov-jdk15on', '1.67') { because 'MIT license' }

    allowDependency('org.vitrivr', 'cottontaildb', '0.13.3') { because 'MIT license' }
    allowDependency('org.vitrivr', 'cottontaildb-proto', '0.13.0') { because 'MIT license' }

    allowDependency('javax.annotation', 'javax.annotation-api', '1.3.2') { because 'CDDL license' }
}
