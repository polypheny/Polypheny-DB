plugins {
    id 'java'
    id "com.google.protobuf" version "0.9.4"
}

apply plugin: "com.google.protobuf"

group "org.polypheny"

configurations {
    tests {
        extendsFrom testRuntimeOnly
    }
}

repositories {
    mavenCentral()
}

dependencies {

    compileOnly project(path: ':core')
    compileOnly project(path: ':dbms')
    compileOnly project(path: ':plugins:sql-language')
    compileOnly project(path: ':plugins:mql-language')
    testImplementation project(path: ':plugins:mql-language')
    testImplementation project(path: ":dbms", configuration: "test")
    testImplementation project(path: ':dbms')
    testImplementation project(path: ":core", configuration: "tests")
    testImplementation project(path: ':core')

    compileOnly project(":monitoring")
    // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.23.4'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

def generatedDirName = "${project.buildDir}/generated"

// compile protos on build
compileJava.dependsOn(generateProto)

extractIncludeProto {
    dependsOn(':core:generateVersionProperties')
    dependsOn(':information:compileJava')
    dependsOn(':core:compileJava')
    dependsOn(':webui:compileJava')
    dependsOn(':monitoring:compileJava')
    dependsOn(':dbms:compileJava')
    dependsOn(':plugins:sql-language:compileJava')
    dependsOn(':plugins:mql-language:compileJava')
}


protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.12"
    }
    generatedFilesBaseDir = "${generatedDirName}/proto"
}

sourceSets {
    main {
        proto {
            srcDir "${protobuf.generatedFilesBaseDir}/proto/main/java"
        }
        java {
            srcDirs = ["src/main/java", "build/generated-sources", 'build/generated/source/proto/main/java']
        }
        resources {
            srcDirs = ["src/main/resources"]
        }
        output.resourcesDir = file(project.buildDir.absolutePath + "/classes")
    }
    test {
        java {
            srcDirs = ["src/test/java"]
        }
        resources {
            srcDirs = ["src/test/resources", "src/main/resources"]
        }
        output.resourcesDir = file(project.buildDir.absolutePath + "/test-classes")
    }
}

delombok {
    dependsOn(":monitoring:processResources")
    dependsOn(':plugins:proto-interface:generateProto')
    dependsOn(':plugins:mql-language:processResources')
    dependsOn(':plugins:mql-language:processResources')
}

compileJava {
    dependsOn(":core:processResources")
    dependsOn(":monitoring:processResources")
}

extractIncludeProto {
    dependsOn(":core:compileJava")
    dependsOn(":monitoring:compileJava")
    dependsOn(":plugins:mql-language:compileJava")
    dependsOn(":webui:compileJava")
    dependsOn(":plugins:sql-language:compileJava")
}

/**
 * Tests
 */
test {
    maxHeapSize = "2g" // Increase heap size (default is 512MB)
}

/**
 * JARs
 */
jar {
    manifest {
        attributes "Manifest-Version": "1.0"
        attributes "Copyright": "The Polypheny Project (polypheny.org)"
        attributes "Version": "$project.version"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
    sourcesJar.dependsOn(':plugins:proto-interface:generateProto')
}


/**
 * LICENSEE
 */
licensee {
    allow('Apache-2.0')
}
