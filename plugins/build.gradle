// licensee
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        google()
    }
    dependencies {
        classpath group: "app.cash.licensee", name: "licensee-gradle-plugin", version: licensee_version
        classpath group: "com.jaredsburrows", name: "gradle-license-plugin", version: license_report_version
    }
}

subprojects {
    // licensee
    apply plugin: "app.cash.licensee"
    apply plugin: "com.jaredsburrows.license"


    // jar
    jar {
        manifest {
            attributes 'Plugin-Class': "${pluginClass}",
                    'Plugin-Id': "${pluginId}",
                    'Plugin-Version': "${archiveVersion}",
                    'Plugin-Provider': "${pluginProvider}",
                    'Plugin-Dependencies': "${pluginDependencies}",
                    'Plugin-Url-Path': "${pluginUrlPath}"
        }
    }

    task plugin(type: Jar) {
        archiveBaseName = "plugin-${pluginId}"
        into('classes') {
            with jar
        }
        into('lib') {
            from configurations.runtimeClasspath
        }
        archiveExtension = 'zip'
    }

    task assemblePlugin(type: Copy) {
        from plugin
        into pluginsDir
    }
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}

build.dependsOn project.tasks.assemblePlugins